pkgname=retroarch
pkgver=1.7.6
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=('x86_64')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libdrm' 'libgl' 'libpulse' 'libx11'
         'libxcb' 'libxext' 'libxinerama' 'libxkbcommon' 'libxv' 'libxxf86vm'
         'mesa' 'openal' 'qt5-base' 'sdl2' 'v4l-utils' 'wayland' 'zlib'
         'libass.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
         'libfreetype.so' 'libswresample.so' 'libswscale.so' 'libudev.so'
         'libusb-1.0.so')
makedepends=('vulkan-icd-loader')
optdepends=('python: retroarch-cg2glsl')
backup=('etc/retroarch.cfg')
source=("https://github.com/libretro/RetroArch/archive/v${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
  cd RetroArch-${pkgver}
  ./configure \
    --prefix='/usr' \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl
  make
  make -C libretro-common/audio/dsp_filters
  make -C gfx/video_filters
}

package() {
  cd RetroArch-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}"/usr/lib/retroarch/filters/audio/
  install -Dm 644 gfx/video_filters/*.{filt,so} -t "${pkgdir}"/usr/lib/retroarch/filters/video/
}

